#Get information on Watercoarces in Russia:
SELECT DISTINCT
  ?waterway ?labelRU ?labelEN ?sourceCoords ?mouthCoords ?mouthQid ?length ?gvr ?osm_id #?tributary
  (GROUP_CONCAT(DISTINCT ?tributaryQid; SEPARATOR="; ") AS ?tributaries)  # Aggregate tributary identifiers into list with ; delimiter
WHERE {
  ?waterway wdt:P31/wdt:P279+ wd:Q47521;  
            wdt:P17 wd:Q159;     # located in Russia

  OPTIONAL { ?waterway rdfs:label ?labelEN FILTER (lang(?labelEN) = "en") . } # Waterway name in English
  OPTIONAL { ?waterway rdfs:label ?labelRU FILTER (lang(?labelRU) = "ru") . } # Waterway name in Russian
  OPTIONAL { ?waterway p:P625 [ ps:P625 ?sourceCoords ; pq:P518 wd:Q7376362 ] . } # Source coordinates
  OPTIONAL { ?waterway p:P625 [ ps:P625 ?mouthCoords  ; pq:P518 wd:Q1233637 ] . } # Mouth coordinates         
  OPTIONAL { ?waterway wdt:P403 ?mouthWatercourse. }
  OPTIONAL { ?waterway wdt:P2043 ?length. }
  OPTIONAL { ?waterway wdt:P884 ?gvr. }
  OPTIONAL { ?waterway wdt:P402 ?osm_id. } # OpenStreetMap relation ID
  OPTIONAL { ?waterway wdt:P974 ?tributary. }
  
  # Strip Wikidata identifiers:
  BIND(STRAFTER(STR(?tributary), STR(wd:)) AS ?tributaryQid) .
  BIND(STRAFTER(STR(?mouthWatercourse), STR(wd:)) AS ?mouthQid) .
 
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}

# Group by for gathering tributaries list
GROUP BY ?waterway ?labelRU ?labelEN ?sourceCoords ?mouthCoords ?mouthQid ?length ?gvr ?osm_id
